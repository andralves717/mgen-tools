#! /usr/bin/awk -f

# filter mgen output to show lost packets
# Copyright (C) 2008 Erik Auerswald <auerswald@fg-networking.de>
#
# usage: packet-loss-filter [FILE...]
# lost and reordered packets cannot be discriminated
# useful when just a few packets are lost
#
# packets lost after the last received packet are not counted

/RECV/ {
  split($3, flow, />/)
  split($4, seq, />/)
  if (hist[flow[2]] == "") {
    hist[flow[2]]=seq[2]+1
  } else if (seq[2]>hist[flow[2]]) {
    print "packet " hist[flow[2]] " of flow " flow[2] " lost (or reordered) at " $1
    hist[flow[2]]=seq[2]+1
  } else if (seq[2]<hist[flow[2]]) {
      print "packet " seq[2] " of flow " flow[2] " too late (or duplicated) at " $1
  } else {
    hist[flow[2]]++
  }
}
