#! /usr/bin/awk -f

# analyse an mgen log file for lost packets
# Copyright (C) 2008,2017 Erik Auerswald <auerswald@fg-networking.de>
#
# Permission is hereby granted, free of charge, to any person obtaining a
# copy of this software and associated documentation files (the "Software"),
# to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense,
# and/or sell copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
# OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
# ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
# OTHER DEALINGS IN THE SOFTWARE.
#
# usage: packet-dup-loss-analyzer [FILE...]
#
# print lost or duplicated packets
# packets lost after the last received packet are not counted

# count received packets and determine range of sequence numbers
/RECV/ {
  split($4, flow, />/)
  split($5, seq, />/)

  if (flows[flow[2]] == "") {
    min[flow[2]] = seq[2]
    max[flow[2]] = seq[2]
  } else {
    if (seq[2] < min[flow[2]]) {
      min[flow[2]] = seq[2]
    }
    if (seq[2] > max[flow[2]]) {
      max[flow[2]] = seq[2]
    }
  }
  flows[flow[2],seq[2]]++
  flows[flow[2]]++
}

# print lost and duplicated packets
END {
  for (fl in flows) {
    split(fl, i, SUBSEP)
    flow_numbers[i[1]] = "used"
  }
  for (f in flow_numbers) {
    for (s = min[f]; s <= max[f]; s++) {
      if (flows[f,s] == 0) {
        print "packet " s " from flow " f " lost"
      } else if (flows[f,s] > 1) {
        if (flow_numbers[f] == "used") {
          printf "Flow %d:",f
          flow_numbers[f] = "need_nl"
        }
        print "packet " s " from flow " f " received " flows[f,s] " times"
      }
    }
  }
}
